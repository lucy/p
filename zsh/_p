#compdef p
#autoload

_p_entry() {
	local IFS=$'\n' entries
	entries=($(p -g --pinentry-mode -g cancel l 2>/dev/null | sort))
	if ((${#entries})); then
		_values 'entry name' $entries
	else
		_message -e entry 'entry name'
	fi
}

_p_cmd() {
	local -a sub
	sub=(
		'c:create db'
		'd:delete'
		'e:edit'
		'g:generate'
		'i:insert'
		'l:list'
		'm:move'
		'p:print'
		'x:diff'
	)
	_describe -t commands 'command' sub "$@"
}

_p() {
	local state line
	_arguments \
		'-h[display usage]' \
		'1: :_p_cmd' \
		'*:: :->args'

	if [[ "$state" == args ]]; then
		case $line[1] in
		c) _message 'no more arguments' ;;
		d) _arguments '1:: :_p_entry' ;;
		e) _arguments '1:: :_p_entry' ;;
		g) _arguments '1::entry name' '2::password length' \
			'*::pwgen options' ;;
		i) _arguments '1::entry name' ;;
		l) ;;
		m) _arguments '1:: :_p_entry' '2::entry name' ;;
		p) _arguments '1:: :_p_entry' ;;
		x) _arguments '1::from commit' '2::to commit' ;;
		*) _message 'invalid command' ;;
		esac
	fi
}

_p
