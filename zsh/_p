#compdef p
#autoload

_p_entry() {
	local IFS=$'\n' entries
	entries=($(p -g '--pinentry-mode cancel' l 2>/dev/null | sort))
	if ((${#entries})); then
		_values 'entry name' $entries
	else
		_message -e entry 'entry name'
	fi
}

_p_cmd() {
	local -a sub
	sub=(
		'c:create db'
		'l:list'
		'p:print'
		'i:insert'
		'd:delete'
		'm:move'
	)
	_describe -t commands 'command' sub "$@"
}

_p() {
	local state line
	_arguments \
		'-h[display usage]' \
		'1: :_p_cmd' \
		'*:: :->args'

	if [[ "$state" == args ]]; then
		case $line[1] in
		c) _message 'no more arguments' ;;
		l) _message 'no more arguments' ;;
		p) _arguments '1:: :_p_entry' ;;
		i) _arguments '1::entry name' ;;
		d) _arguments '1:: :_p_entry' ;;
		m) _arguments '1:: :_p_entry' '2::entry name' ;;
		*) _message 'invalid command' ;;
		esac
	fi
}

_p
